print ":::::::: Initializing Houdini Plugin Scripts :::::::::";

scriptJob -event "SelectionChanged" "houdiniPluginOnSelectionChanged";




//This whole ugliness with callbacks on selection changed 
//has to do with a bug in Maya where AETemplates don't behave
//properly when the underlying attributes are dynamically changing.  It tends to assume
//that the attrs are static.  So when you select a different Houdini Asset, the 
//Attribute editors are messed up, and can only be fixed by a call to
//refreshEditorTemplates.

global string $gLastSelectedHoudiniAsset = "";

global proc houdiniPluginOnSelectionChanged()
{
    global string $gLastSelectedHoudiniAsset;
    int $foundHoudiniAsset = 0;
    int $foundDiffHoudiniAsset = 0;
    string $currSel[] = `ls -sl`;
    for( $ii = 0; $ii < size($currSel); $ii++)
    {
        string $sel = $currSel[$ii];
        if(`nodeType $sel` == "houdiniAsset")
        {
            $foundHoudiniAsset = 1;
            if( $sel != $gLastSelectedHoudiniAsset )
            {
                $foundDiffHoudiniAsset = 1;
                $gLastSelectedHoudiniAsset = $sel;
            }
            break;
        }
        string $connections[] = `listConnections $sel`;

        for( $jj = 0; $jj < size($connections); $jj++)
        {
            string $conn = $connections[$jj];
            if(`nodeType $conn` == "houdiniAsset")
            {
                $foundHoudiniAsset = 1;

                if( $conn != $gLastSelectedHoudiniAsset )
                {
                    $foundDiffHoudiniAsset = 1;
                    $gLastSelectedHoudiniAsset = $conn;
                }

                break;
            }
        }

        if($foundHoudiniAsset)
            break;
        
    }
    if($foundHoudiniAsset && $foundDiffHoudiniAsset)
        refreshEditorTemplates;
}
