source createAndAssignShader;

proc string getTransform( string $shape )
{
    string $transform = "";

    if ( "transform" != `nodeType $shape` )
        // If given node is already a transform, just pass on through
    {
        string $parents[] = `listRelatives -fullPath -parent $shape`;
        $transform = $parents[0];
    }

    if ($transform == "")
        return $transform;
    return `substring $transform 2 (size($transform))`;
}

global proc makeAsset(string $fileName) {
    string $asset = `createNode hAsset`;
    setAttr -type "string" ($asset + ".fileName") $fileName;
    setAttr -lock on ($asset + ".fileName");

    // TODO: this is a hack, getting the attribute once sometimes returns
    // wrong result, getting it again seem to return correct result.
    int $numObjects = `getAttr ($asset + ".numberOfObjects")`;
    $numObjects = `getAttr ($asset + ".numberOfObjects")`;

    string $transforms[] = {};
    print $numObjects;

    for ($i=0; $i<$numObjects; $i++) {
        string $mesh = $asset + "Mesh" + $i;
        createNode mesh -n $mesh;
        connectAttr ($asset + ".meshes[" + $i + "]") ($mesh + ".inMesh");

        string $transform = `getTransform $mesh`;
        $transforms[size($transforms)] = $transform;
        connectAttr ($asset + ".transforms[" + $i + "].translate") ($transform + ".translate");
        connectAttr ($asset + ".transforms[" + $i + "].rotate") ($transform + ".rotate");
        connectAttr ($asset + ".transforms[" + $i + "].scale") ($transform + ".scale");

        string $tmp = "assignCreatedShader %type \"" + $mesh + "\" %node \"" + $mesh + "\"";
        createRenderNodeCB -asShader "surfaceShader" lambert $tmp;

    }

    if ($numObjects <= 0)
        return;

    // group the objects to be under one transform
    string $cmd = "group -n " + $asset + "Transform";
    for ($i=0; $i<size($transforms); $i++) {
        $cmd += " ";
        $cmd += $transforms[$i];
    }
    eval $cmd;

    refreshEditorTemplates;

}

